"""
Workflow purpose
----------------
Study the computed robust independent components.

"""

import os
import numpy as np

##### VARIABLES #####
ROOT = os.path.dirname(os.path.dirname(os.getcwd()))
DATA_DIR = os.path.join(ROOT,'data')
RAW_DIR = os.path.join(DATA_DIR,'raw')
PREP_DIR = os.path.join(DATA_DIR,'prep')
RESULTS_SIGN_DIR = os.path.join(ROOT,'results','benchmark_sign_inference')
RESULTS_DIR = os.path.join(ROOT,'results','case_study')

MSIGDB_DIR = os.path.join(RAW_DIR,'MSigDB','msigdb_v7.4','msigdb_v7.4_files_to_download_locally','msigdb_v7.4_GMTs')

DATASETS = ['LGG','Sastry2019']
ALGORITHMS = ['icasso','robustica_pca']

##### RULES #####
rule all:
    input:
        # gene set enrichment analysis
        expand(os.path.join(RESULTS_DIR,"files","gsea","{dataset}","{algorithm}.tsv.gz"), dataset=["LGG"], algorithm=ALGORITHMS),
        
        # make figures
        expand(os.path.join(RESULTS_DIR,'figures','interpretation','{dataset}'), dataset=['LGG'])
        
            
rule gsea:
    input:
        S = os.path.join(RESULTS_SIGN_DIR,'files',"{dataset}",'{algorithm}-S.tsv.gz'),
        msigdb_dir = MSIGDB_DIR
    output:
        os.path.join(RESULTS_DIR,"files","gsea","{dataset}","{algorithm}.tsv.gz")
    params:
        features_col = "sample"
    shell:
        """
        nice Rscript scripts/run_gsea.R \
                --S_file={input.S} \
                --msigdb_dir={input.msigdb_dir} \
                --output_file={output} \
                --features_col={params.features_col}
        """


rule figures_case_study:
    input:
        S = os.path.join(RESULTS_DIR,'files','cluster_iterations','{dataset}','robustica_pca','S.tsv.gz'),
        A = os.path.join(RESULTS_DIR,'files','cluster_iterations','{dataset}','robustica_pca','A.tsv.gz'),
        stats = os.path.join(RESULTS_DIR,'files','cluster_iterations','{dataset}','robustica_pca','stats.tsv.gz'),
        genexpr = os.path.join(PREP_DIR,'genexpr','{dataset}.tsv.gz'),
        snv = os.path.join(PREP_DIR,'snv','{dataset}.tsv.gz'),
        metadata = os.path.join(PREP_DIR,'metadata','{dataset}.tsv'),
        sample_indices = os.path.join(PREP_DIR,'sample_indices','{dataset}.tsv'),
        msigdb_dir = MSIGDB_DIR
    output:
        directory(os.path.join(RESULTS_DIR,'figures','interpretation','{dataset}'))
    shell:
        """
        Rscript scripts/figures_interpret_case_study.R \
                    --S_file={input.S} \
                    --A_file={input.A} \
                    --genexpr_file={input.genexpr} \
                    --snv_file={input.snv} \
                    --metadata_file={input.metadata} \
                    --stats_file={input.stats} \
                    --sample_indices_file={input.sample_indices} \
                    --msigdb_dir={input.msigdb_dir} \
                    --figs_dir={output}
        """
        
